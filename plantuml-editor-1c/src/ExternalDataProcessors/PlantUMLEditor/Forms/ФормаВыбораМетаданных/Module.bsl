///////////////////////////////////////////////////////////////////////////////////////////////////////
// Copyright (c) 2022-2026, ООО 1С-Рарус
// Все права защищены. Эта программа и сопроводительные материалы предоставляются
// в соответствии с условиями лицензии Attribution-ShareAlike 4.0 International (CC BY-SA 4.0)
// Текст лицензии доступен по ссылке:
// https://creativecommons.org/licenses/by-sa/4.0/legalcode
///////////////////////////////////////////////////////////////////////////////////////////////////////

#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)

	СформироватьСписокОбъектовКонфигурации();

КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)

	Для Каждого ВидОбъектаКонфигурации Из ОбъектыКонфигурации.ПолучитьЭлементы() Цикл

		Элементы.ОбъектыКонфигурации.Развернуть(ВидОбъектаКонфигурации.ПолучитьИдентификатор(), Истина);

	КонецЦикла;

КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовТаблицыФормыОбъектыКонфигурации

&НаКлиенте
Процедура ОбъектыКонфигурацииВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)

	Если Поле = "ОбъектыКонфигурацииПометка" Тогда

		Возврат;

	КонецЕсли;

	СтандартнаяОбработка = Ложь;
	ОбработатьВыборОбъектаМетаданных(Истина);

КонецПроцедуры

&НаКлиенте
Процедура ОбъектыКонфигурацииПометкаПриИзменении(Элемент)

	ОбработатьВыборОбъектаМетаданных();

КонецПроцедуры // ОбъектыКонфигурацииПометкаПриИзменении()

#КонецОбласти
#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура Выбрать(Команда)

	ОбъектыВыбора = Новый Массив();
	Для Каждого ВыбранныйОбъект Из ВыбранныеОбъекты Цикл

		СтрокаОбъекта = ВыбранныйОбъект.Значение;
		Если Не СтрокаОбъекта.Пометка Тогда

			Продолжить;

		КонецЕсли;

		ОбъектВыбора = Новый Соответствие();;
		ОбъектВыбора.Вставить("ПутьКМетаданным", СтрШаблон(
			"%1.%2",
			СтрокаОбъекта.ПолучитьРодителя().ПредставлениеОбъекта,
			СтрокаОбъекта.ИмяОбъекта));
		ОбъектВыбора.Вставить("ВидОбъекта", СтрокаОбъекта.ПолучитьРодителя().ПредставлениеОбъекта);
		ОбъектВыбора.Вставить("ИмяОбъекта", СтрокаОбъекта.ИмяОбъекта);
		ОбъектыВыбора.Добавить(ОбъектВыбора);

	КонецЦикла;

	Если ОбъектыВыбора.Количество() = 0 Тогда

		ОбработкаОповещения = Новый ОписаниеОповещения("Подключаемый_ЗавершениеБезВыбора", ЭтотОбъект);
		ПоказатьВопрос(
			ОбработкаОповещения,
			Нстр("ru = 'Не выбран объект конфигурации. Продолжить?'"),
			РежимДиалогаВопрос.ДаНет);
		Возврат;

	КонецЕсли;

	Результат = Новый Структура();
	Результат.Вставить("ВыводитьСвязиМеждуОбъектами", ВыводитьСвязиМеждуОбъектами);
	Результат.Вставить("СгруппироватьПоВидамМетаданных", СгруппироватьПоВидамМетаданных);
	Результат.Вставить("ВыбранныеОбъекты", ОбъектыВыбора);

	Закрыть(Результат);

КонецПроцедуры // Выбрать()

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Процедура СформироватьСписокОбъектовКонфигурации()

	ВидыОбъектов = ВидыОбъектовКонфигурации();
	Для Каждого ТекущийВидОбъекта Из ВидыОбъектов Цикл

		НовыйВидОбъектов = ОбъектыКонфигурации.ПолучитьЭлементы().Добавить();
		НовыйВидОбъектов.ИмяОбъекта = ТекущийВидОбъекта.Значение;
		НовыйВидОбъектов.ПредставлениеОбъекта = ТекущийВидОбъекта.Представление;
		НовыйВидОбъектов.КартинкаОбъекта = ТекущийВидОбъекта.Картинка;
		НовыйВидОбъектов.ЭтоГруппа = Истина;

		Для Каждого ТекущийОбъект Из Метаданные[ТекущийВидОбъекта.Значение] Цикл

			НоваяСтрокаОбъекта = НовыйВидОбъектов.ПолучитьЭлементы().Добавить();
			НоваяСтрокаОбъекта.ИмяОбъекта = ТекущийОбъект.Имя;
			НоваяСтрокаОбъекта.ПредставлениеОбъекта = ТекущийОбъект.Имя;
			НоваяСтрокаОбъекта.КартинкаОбъекта = ТекущийВидОбъекта.Картинка;

		КонецЦикла;

	КонецЦикла;

КонецПроцедуры // СформироватьСписокОбъектовКонфигурации()

&НаСервере
Функция ВидыОбъектовКонфигурации()

	ВидыОбъектов = Новый СписокЗначений();
	ВидыОбъектов.Добавить("Справочники", НСтр("ru = 'Справочники'"),, БиблиотекаКартинок.Справочник);
	ВидыОбъектов.Добавить("Документы", НСтр("ru = 'Документы'"),, БиблиотекаКартинок.Документ);
	ВидыОбъектов.Добавить("РегистрыСведений", НСтр("ru = 'РегистрыСведений'"),, БиблиотекаКартинок.РегистрСведений);
	ВидыОбъектов.Добавить(
		"РегистрыНакопления",
		НСтр("ru = 'РегистрыНакопления'"),,
		БиблиотекаКартинок.РегистрНакопления);
		ВидыОбъектов.Добавить("Перечисления", НСтр("ru = 'Перечисления'"),, БиблиотекаКартинок.Перечисление);
	ВидыОбъектов.Добавить(
		"ПланыВидовХарактеристик",
		НСтр("ru = 'ПланыВидовХарактеристик'"),,
		БиблиотекаКартинок.ПланВидовХарактеристик);
	ВидыОбъектов.Добавить(
		"ПланыСчетов",
		НСтр("ru = 'ПланыСчетов'"),,
		БиблиотекаКартинок.ПланСчетов);
	ВидыОбъектов.Добавить(
		"ПланыВидовРасчета",
		НСтр("ru = 'ПланыВидовРасчета'"),,
		БиблиотекаКартинок.ПланВидовРасчета);
	ВидыОбъектов.Добавить(
		"РегистрыБухгалтерии",
		НСтр("ru = 'РегистрыБухгалтерии'"),,
		БиблиотекаКартинок.РегистрБухгалтерии);
	ВидыОбъектов.Добавить(
		"РегистрыРасчета",
		НСтр("ru = 'РегистрыРасчета'"),,
		БиблиотекаКартинок.РегистрРасчета);

	Возврат ВидыОбъектов;

КонецФункции // ВидыОбъектовКонфигурации()

&НаКлиенте
Процедура Подключаемый_ЗавершениеБезВыбора(Ответ, ДополнительныеПараметры = Неопределено) Экспорт

	Если Ответ = КодВозвратаДиалога.Да Тогда

		Закрыть();

	КонецЕсли;

КонецПроцедуры // Подключаемый_ЗавершениеБезВыбора()

&наКлиенте
Процедура ОбработатьВыборОбъектаМетаданных(УстановитьПометку = Ложь)

	ТекущаяСтрока = Элементы.ОбъектыКонфигурации.ТекущаяСтрока;
	СтрокаОбъекта = ОбъектыКонфигурации.НайтиПоИдентификатору(ТекущаяСтрока);

	Если УстановитьПометку Тогда

		СтрокаОбъекта.Пометка = Не СтрокаОбъекта.Пометка;

	КонецЕсли;

	Если СтрокаОбъекта.ЭтоГруппа Тогда

		Для Каждого ОбъектМетаданных Из СтрокаОбъекта.ПолучитьЭлементы() Цикл

			ОбъектМетаданных.Пометка = СтрокаОбъекта.Пометка;
			ИзменитьСписокВыбранныхОбъектов(ОбъектМетаданных, СтрокаОбъекта.Пометка);

		КонецЦикла;

	Иначе

		ИзменитьСписокВыбранныхОбъектов(СтрокаОбъекта, СтрокаОбъекта.Пометка);

	КонецЕсли;

КонецПроцедуры // ОбработатьВыборОбъектаМетаданных()

&НаКлиенте
Процедура ИзменитьСписокВыбранныхОбъектов(ОбъектМетаданных, Пометка)

	Если Пометка И ВыбранныеОбъекты.НайтиПоЗначению(ОбъектМетаданных) = Неопределено Тогда

		ВыбранныеОбъекты.Добавить(ОбъектМетаданных);

	ИначеЕсли Не Пометка И ВыбранныеОбъекты.НайтиПоЗначению(ОбъектМетаданных) <> Неопределено Тогда

		ВыбранныеОбъекты.Удалить(ВыбранныеОбъекты.НайтиПоЗначению(ОбъектМетаданных));

	КонецЕсли;

КонецПроцедуры // ИзменитьСписокВыбранныхОбъектов()

#КонецОбласти
