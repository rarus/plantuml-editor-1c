///////////////////////////////////////////////////////////////////////////////////////////////////////
// Copyright (c) 2022-2026, ООО 1С-Рарус
// Все права защищены. Эта программа и сопроводительные материалы предоставляются
// в соответствии с условиями лицензии Attribution-ShareAlike 4.0 International (CC BY-SA 4.0)
// Текст лицензии доступен по ссылке:
// https://creativecommons.org/licenses/by-sa/4.0/legalcode
///////////////////////////////////////////////////////////////////////////////////////////////////////

#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)

	ВывестиСведенияОПрограмме();

КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Функция ВывестиСведенияОПрограмме()

	СведенияОПрограмме = ПолучитьСведенияОПрограмме();
	Если ТипЗнч(СведенияОПрограмме)<>Тип("Структура") ИЛИ ПустаяСтрока(СведенияОПрограмме.Авторы) Тогда
		ВызватьИсключение НСтр("ru = 'Нарушен формат указания сведений о программе. См. https://its.1c.ru/db/bsp311doc#content:31:hdoc'");
	КонецЕсли;

	Элементы.НазваниеПрограммы .Заголовок = СведенияОПрограмме.Наименование;
	Элементы.ВерсияПрограммы   .Заголовок = СведенияОПрограмме.Версия;
	Элементы.РепозиторийПроекта.Заголовок = Новый ФорматированнаяСтрока(СведенияОПрограмме.РепозиторийПроекта,,,, СведенияОПрограмме.РепозиторийПроекта);

	Авторы = СтрРазделить(СтрЗаменить(СведенияОПрограмме.Авторы, ",", ";"), ";", ЛОЖЬ);
	Если Авторы.Количество() > 1 Тогда
		Элементы.НадписьАвторы.Заголовок = НСтр("ru = 'Авторы'");
	КонецЕсли;

	Для Сч = 0 По Авторы.ВГраница() Цикл

		ИмяЭлемента = "Автор"+Формат(Сч,"ЧН=0; ЧГ=0");

		Элемент = Элементы.Найти(ИмяЭлемента);
		Если Элемент=Неопределено Тогда
			Элемент = ЭтотОбъект.Элементы.Добавить(ИмяЭлемента, Тип("ДекорацияТекстФормы"), Элементы.ГруппаПереченьАвторов);
		КонецЕсли;

		Элемент.Заголовок = СокрЛП(Авторы[Сч]);

	КонецЦикла;

КонецФункции // ВывестиСведенияОПрограмме()

&НаСервере
Функция ПолучитьСведенияОПрограмме()

	ИмяОбъекта = СтрРазделить(ЭтотОбъект.ИмяФормы, ".", ЛОЖЬ);
	Если ИмяОбъекта.Количество() < 3 Тогда
		Возврат Неопределено;
	КонецЕсли;

	Если ВРег(ИмяОбъекта[0]) = ВРег("ВнешняяОбработка") ИЛИ ВРег(ИмяОбъекта[0]) = ВРег("ExternalDataProcessor") Тогда
		ТекущийОбъект = ВнешниеОбработки.Создать(ИмяОбъекта[1]);

	ИначеЕсли ВРег(ИмяОбъекта[0]) = ВРег("ВнешнийОтчет") ИЛИ ВРег(ИмяОбъекта[0]) = ВРег("ExternalReport") Тогда
		ТекущийОбъект = ВнешниеОтчеты.Создать(ИмяОбъекта[1]);
	Иначе
		Возврат Неопределено;
	КонецЕсли;

	Попытка
		СведенияОПрограмме = ТекущийОбъект.СведенияОВнешнейОбработке();
		Возврат СведенияОПрограмме;
	Исключение
		ПричинаОшибки = ИнформацияОбОшибке();
		Возврат Неопределено;
	КонецПопытки;

КонецФункции // ПолучитьСведенияОПрограмме()

#КонецОбласти