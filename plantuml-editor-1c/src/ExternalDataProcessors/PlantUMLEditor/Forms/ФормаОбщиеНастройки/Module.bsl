///////////////////////////////////////////////////////////////////////////////////////////////////////
// Copyright (c) 2022-2026, ООО 1С-Рарус
// Все права защищены. Эта программа и сопроводительные материалы предоставляются
// в соответствии с условиями лицензии Attribution-ShareAlike 4.0 International (CC BY-SA 4.0)
// Текст лицензии доступен по ссылке:
// https://creativecommons.org/licenses/by-sa/4.0/legalcode
///////////////////////////////////////////////////////////////////////////////////////////////////////

#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)

	АдресСервераРендеринга = Параметры.АдресСервераРендеринга;
	СписокВыбора = Элементы.АдресСервераРендеринга.СписокВыбора;
	УстановитьДоступныеАдресаСервераРендеринга(СписокВыбора);

КонецПроцедуры // ПриСозданииНаСервере()

#КонецОбласти


#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура ИнформацияОСервереНажатие(Элемент)

	ДополнительныеПараметры = Новый Структура();
	ДополнительныеПараметры.Вставить("ВывестиВерсиюСервера");
	ПередатьИзмененияНастроек(ДополнительныеПараметры);

КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура ЗаписатьИЗакрыть(Команда)

	ПередатьИзмененияНастроек();

КонецПроцедуры // ЗаписатьИЗакрыть()

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаКлиенте
Процедура ПроверитьЗаполнениеОбяхательныхНастроек(Отказ)

	Если ПустаяСтрока(АдресСервераРендеринга) Тогда

		СообщениеПользователю = Новый СообщениеПользователю();
		СообщениеПользователю.Текст = НСтр("ru = 'Не заполнен адрес сервера рендеринга'");
		СообщениеПользователю.Поле = "АдресСервераРендеринга";
		СообщениеПользователю.Сообщить();
		Отказ = Истина;

	КонецЕсли;

КонецПроцедуры // ПроверитьЗаполнениеОбяхательныхНастроек()

&НаСервере
Функция УстановитьДоступныеАдресаСервераРендеринга(СписокДляВыбора)

	СписокДляВыбора.Очистить();
	СписокДляВыбора.Добавить("plantuml.rarus.ru");
	СписокДляВыбора.Добавить("www.plantuml.com");

КонецФункции // УстановитьДоступныеАдресаСервераРендеринга()

&НаКлиенте
Процедура ПередатьИзмененияНастроек(ДополнительныеПараметры = Неопределено)

	Отказ = Ложь;
	ПроверитьЗаполнениеОбяхательныхНастроек(Отказ);

	Если Отказ Тогда

		Возврат;

	КонецЕсли;

	Результат = Новый Структура();
	Результат.Вставить("АдресСервераРендеринга", АдресСервераРендеринга);
	Результат.Вставить("ДополнительныеПараметры", ДополнительныеПараметры);

	Закрыть(Результат);

КонецПроцедуры // ПередатьИзмененияНастроек()

#КонецОбласти
