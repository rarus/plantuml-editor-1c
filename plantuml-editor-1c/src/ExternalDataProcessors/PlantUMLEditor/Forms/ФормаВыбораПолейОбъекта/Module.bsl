///////////////////////////////////////////////////////////////////////////////////////////////////////
// Copyright (c) 2022-2026, ООО 1С-Рарус
// Все права защищены. Эта программа и сопроводительные материалы предоставляются
// в соответствии с условиями лицензии Attribution-ShareAlike 4.0 International (CC BY-SA 4.0)
// Текст лицензии доступен по ссылке:
// https://creativecommons.org/licenses/by-sa/4.0/legalcode
///////////////////////////////////////////////////////////////////////////////////////////////////////

#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)

	Заголовок = СтрШаблон("Выбор полей объекта %1", Параметры.ИмяОбъекта);
	РанееВыбранныеРеквизиты = Параметры.ВыбранныеРеквизиты;
	ЗаполнитьПоляОбъекта(Параметры.ПутьКМетаданным);

КонецПроцедуры // ПриСозданииНаСервере()

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура Выбрать(Команда)

	Результат = СформироватьСписокВыбранныхПолей();

	Если Результат = Неопределено Тогда

		Возврат;

	КонецЕсли;

	Закрыть(Результат);

КонецПроцедуры // Выбрать()

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Процедура ЗаполнитьПоляОбъекта(ПутьКМетаданным)

	МетаданныеОбъекта = СтрРазделить(ПутьКМетаданным, ".");
	ДлинаПутиОбъекта = 2;
	ДлинаПутиКТабличнойЧасти = 4;

	Если МетаданныеОбъекта.Количество() = ДлинаПутиОбъекта Тогда

		ИнициализироватьРеквизитыОбъекта(МетаданныеОбъекта[0], МетаданныеОбъекта[1]);

	ИначеЕсли МетаданныеОбъекта.Количество() = ДлинаПутиКТабличнойЧасти Тогда

		ИнициализироватьРеквизитыОбъектаТабличнойЧасти(
			МетаданныеОбъекта[0],
			МетаданныеОбъекта[1],
			МетаданныеОбъекта[3]);

	КонецЕсли;

КонецПроцедуры // ЗаполнитьПоляОбъекта()

&НаСервере
Процедура ИнициализироватьРеквизитыОбъекта(ВидОбъекта, ИмяОбъекта)

	Попытка

		МетаданныеОбъекта = Метаданные[ВидОбъекта][ИмяОбъекта];

	Исключение

		ВызватьИсключение НСтр("ru = 'Выбранный объект не найден в метаданных конфигурации.'");

	КонецПопытки;

	// Определимся с составом реквизитов.
	Если ЭтоРегистр(МетаданныеОбъекта) Тогда

		ДополнитьПоляРеквизитами(МетаданныеОбъекта, "СтандартныеРеквизиты", БиблиотекаКартинок.Реквизит);
		ДополнитьПоляРеквизитами(МетаданныеОбъекта, "Измерения", БиблиотекаКартинок.Измерение);
		ДополнитьПоляРеквизитами(МетаданныеОбъекта, "Ресурсы", БиблиотекаКартинок.Ресурс);
		ДополнитьПоляРеквизитами(МетаданныеОбъекта, "Реквизиты", БиблиотекаКартинок.Реквизит);

	ИначеЕсли Метаданные.Перечисления.Содержит(МетаданныеОбъекта) Тогда

		ДополнитьЗначениямиПеречисления(МетаданныеОбъекта);

	Иначе

		ДополнитьПоляРеквизитами(МетаданныеОбъекта, "СтандартныеРеквизиты", БиблиотекаКартинок.Реквизит);
		ДополнитьПоляРеквизитами(МетаданныеОбъекта, "Реквизиты", БиблиотекаКартинок.Реквизит);
		ДополнитьПоляОбщимиРеквизитами(МетаданныеОбъекта);
		ДополнитьПоляРеквизитами(МетаданныеОбъекта, "ТабличныеЧасти", БиблиотекаКартинок.ВложеннаяТаблица);

	КонецЕсли;

КонецПроцедуры

&НаСервере
Процедура ИнициализироватьРеквизитыОбъектаТабличнойЧасти(ВидОбъекта, ИмяОбъекта, ИмяТабличнойЧасти)

	Попытка

		Для Каждого Реквизит Из Метаданные[ВидОбъекта][ИмяОбъекта].ТабличныеЧасти[ИмяТабличнойЧасти].Реквизиты Цикл

			НоваяСтрока = СписокПолей.Добавить();
			НоваяСтрока.ВидПоля = Реквизит.Тип;
			НоваяСтрока.Поле = Реквизит.Имя;
			НоваяСтрока.Картинка = БиблиотекаКартинок.Реквизит;
			НоваяСтрока.Пометка = РанееВыбранныеРеквизиты.НайтиПоЗначению(Реквизит.Имя) <> Неопределено;

		КонецЦикла;

	Исключение

		ВызватьИсключение НСтр("ru = 'Выбранный объект не найден в метаданных конфигурации.'");

	КонецПопытки;

КонецПроцедуры // ИнициализироватьРеквизитыОбъектаТабличнойЧасти()

&НаСервере
Процедура ДополнитьПоляРеквизитами(МетаданныеОбъекта, ИмяПолей, Картинка)

	ИсключаемыеРеквизиты = ИсключаемыеСтандартныеРеквизиты();

	Для Каждого Реквизит Из МетаданныеОбъекта[ИмяПолей] Цикл

		Если ИмяПолей = "СтандартныеРеквизиты"
			И ИсключаемыеРеквизиты.Найти(Реквизит.Имя) <> Неопределено Тогда

			Продолжить;

		КонецЕсли;

		НоваяСтрока = СписокПолей.Добавить();

		Если ИмяПолей <> "ТабличныеЧасти" Тогда

			НоваяСтрока.ВидПоля = Реквизит.Тип;

		КонецЕсли;

		НоваяСтрока.Поле = Реквизит.Имя;
		НоваяСтрока.Картинка = Картинка;
		НоваяСтрока.Пометка = РанееВыбранныеРеквизиты.НайтиПоЗначению(Реквизит.Имя) <> Неопределено;

	КонецЦикла;

КонецПроцедуры // ДополнитьПоляРеквизитами()

&НаСервере
Процедура ДополнитьЗначениямиПеречисления(МетаданныеОбъекта)

	Для Каждого ЗначениеПеречисления Из МетаданныеОбъекта.ЗначенияПеречисления Цикл

		НоваяСтрока = СписокПолей.Добавить();
		НоваяСтрока.Поле = ЗначениеПеречисления.Имя;
		НоваяСтрока.Картинка = БиблиотекаКартинок.Реквизит;
		НоваяСтрока.Пометка = РанееВыбранныеРеквизиты.НайтиПоЗначению(ЗначениеПеречисления.Имя) <> Неопределено;

	КонецЦикла;

КонецПроцедуры // ДополнитьПоляРеквизитами()

&НаСервереБезКонтекста
Функция ИсключаемыеСтандартныеРеквизиты()

	ИсключаемыеРеквизиты = Новый Массив();
	ИсключаемыеРеквизиты.Добавить("Ссылка");
	ИсключаемыеРеквизиты.Добавить("Код");
	ИсключаемыеРеквизиты.Добавить("Наименование");
	ИсключаемыеРеквизиты.Добавить("Родитель");
	ИсключаемыеРеквизиты.Добавить("ЭтоГруппа");
	ИсключаемыеРеквизиты.Добавить("ПометкаУдаления");
	ИсключаемыеРеквизиты.Добавить("Предопределенный");
	ИсключаемыеРеквизиты.Добавить("ИмяПредопределенныхДанных");
	ИсключаемыеРеквизиты.Добавить("Дата");
	ИсключаемыеРеквизиты.Добавить("Номер");
	ИсключаемыеРеквизиты.Добавить("Проведен");
	ИсключаемыеРеквизиты.Добавить("Период");

	Возврат ИсключаемыеРеквизиты;

КонецФункции // ИсключаемыеСтандартныеРеквизиты()

&НаСервере
Процедура ДополнитьПоляОбщимиРеквизитами(МетаданныеОбъекта)

	Для Каждого Реквизит Из Метаданные.ОбщиеРеквизиты Цикл

		ЗначениеСостава = Реквизит.Состав.Найти(МетаданныеОбъекта);

		Если ЗначениеСостава = Неопределено 
			ИЛИ (НЕ ЗначениеСостава.Использование =
				Метаданные.СвойстваОбъектов.ИспользованиеОбщегоРеквизита.Использовать) Тогда

			Продолжить;

		КонецЕсли;

		НоваяСтрока = СписокПолей.Добавить();
		НоваяСтрока.ВидПоля = Реквизит.Тип;
		НоваяСтрока.Поле = Реквизит.Имя;
		НоваяСтрока.Картинка = БиблиотекаКартинок.Реквизит;
		НоваяСтрока.Пометка = РанееВыбранныеРеквизиты.НайтиПоЗначению(Реквизит.Имя) <> Неопределено;

	КонецЦикла;

КонецПроцедуры // ДополнитьПоляРеквизитами()

&НаСервереБезКонтекста
Функция ЭтоРегистр(ОбъектМетаданных)

	Возврат Метаданные.РегистрыБухгалтерии.Содержит(ОбъектМетаданных)
		Или Метаданные.РегистрыНакопления.Содержит(ОбъектМетаданных)
		Или Метаданные.РегистрыРасчета.Содержит(ОбъектМетаданных)
		Или Метаданные.РегистрыСведений.Содержит(ОбъектМетаданных);

КонецФункции // ЭтоРегистр()

&НаКлиенте
Функция СформироватьСписокВыбранныхПолей()

	УсловиеПоиска = Новый Структура("Пометка", Истина);
	НайденныеВыбранныеПоля = СписокПолей.НайтиСтроки(УсловиеПоиска);

	Если НайденныеВыбранныеПоля.Количество() = 0 Тогда

		ОбработчикОповещения = Новый ОписаниеОповещения("ЗавершениеПродолженияБезВыбораПолей", ЭтотОбъект);
		ПоказатьВопрос(
			ОбработчикОповещения,
			НСтр("ru = 'Не выбрано ни одного поля объекта. Продолжить?'"),
			РежимДиалогаВопрос.ДаНет);
		Возврат Неопределено;

	КонецЕсли;

	Результат = Новый Соответствие();

	Для Каждого ВыбранноеПоле Из НайденныеВыбранныеПоля Цикл

		Результат.Вставить(ВыбранноеПоле.Поле, ВыбранноеПоле.ВидПоля);

	КонецЦикла;

	Возврат Результат;

КонецФункции // СформироватьСписокВыбранныхПолей()

&НаКлиенте
Процедура ЗавершениеПродолженияБезВыбораПолей(Ответ, ДополнительныеПараметры = Неопределено) Экспорт

	Если Ответ = КодВозвратаДиалога.Да Тогда

		Закрыть();

	КонецЕсли;

КонецПроцедуры

#КонецОбласти
